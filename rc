#!/usr/bin/env zsh
# .zshrc file Â© 2011-2014 Christian Kellner <christian@kellner.me>

##################################################################
# Initialization

ZDIR=$HOME/.zsh
fpath=($fpath $ZDIR/functions /usr/local/share/zsh/site-functions)
autoload -U "$ZDIR/functions"/*(N:t) >/dev/null
zshrc_load_status 'executing .zsh/rc...'


##################################################################
# Basic setup

autoload -U colors && colors
setenv() { export $1=$2 }

setopt nobeep		# BEEEEEEEEEEEP!
setopt correct

setopt autocd           # change to dirs without cd
setopt auto_name_dirs
setopt auto_pushd
setopt pushd_ignore_dups


# jump to each element in a path with m-f m-b, same for kill-word, etc.
export WORDCHARS=''

export REPORTTIME=10

case $TERM in
xterm*|screen*|rxvt*)
  precmd () { print -Pn "\e]0;%n@%m [%3c]\a"}
  ;;
dumb)
  zshrc_play_dumb
  return
  ;;
esac

##################################################################
# History

export HISTSIZE=500
export SAVEHIST=500
export HISTFILE=~/.zsh_history
setopt hist_ignore_dups
setopt appendhistory
setopt share_history
setopt histignorespace

##################################################################
# Aliases
zshrc_load_status 'loading aliases...'
source $ZDIR/alias

##################################################################
# Key-Bindings
bindkey -e

#   bind special keys according to readline configuration
# eval "$(sed -n 's/^/bindkey /; s/: / /p' /etc/inputrc)"

##################################################################
# Misc

# smart urls
autoload -U url-quote-magic
zle -N self-insert url-quote-magic

#make commandline editable
autoload -U edit-command-line
zle -N edit-command-line
bindkey '\C-x\C-e' edit-command-line

#################################################################
# Plugins

#deterime host for plugins and host adjustments
local host_type="$(uname | tr '[A-Z]' '[a-z]')"

zplugs=(history-substring-search \
        homebrew                 \
        homeshick                \
        forklift                 \
        archey                   \
        pip                      \
        man                      \
        syntax-highlighting      \
        autosuggestions          \
        git-radar                \
        emacsclient              \
        python                   \
	fzf                      \
       )

zshrc_load_zplug $zplugs

##################################################################
# Host specific adjustments

local perhostfile=$ZDIR/os/$host_type

if [[ -f $perhostfile ]]; then
  source $perhostfile
fi

#LS_COLORS got defined per arch in the specific files
alias ls="ls $LS_OPTIONS"

##################################################################
# auto completion tweaks

zshrc_load_status 'loading completion...'
source $ZDIR/completion

##################################################################
# They set us up the prompt ...
zshrc_load_status 'setting the prompt...'
source $ZDIR/prompt

################################################################
# We are done (almost).
echo -n "\r\e[0K"

