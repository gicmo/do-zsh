#!/usr/bin/env zsh

if type "fzf" > /dev/null && [[ $- == *i* ]]; then
local fzf_shell_root

if [[ -d "$GOPATH/src/github.com/junegunn/fzf/shell" ]]; then
  fzf_shell_root="$GOPATH/src/github.com/junegunn/fzf/shell"
elif [[ -d "/usr/local/opt/fzf/shell" ]]; then
  fzf_shell_root="/usr/local/opt/fzf/shell/"
fi

source "${fzf_shell_root}/completion.zsh"

__fzf_use_tmux__() {
  [ -n "$TMUX_PANE" ] && [ "${FZF_TMUX:-0}" != 0 ] && [ ${LINES:-40} -gt 15 ]
}

__fzfcmd() {
  __fzf_use_tmux__ &&
    echo "fzf-tmux -d${FZF_TMUX_HEIGHT:-40%}" || echo "fzf"
}

fzf-history-widget() {
  local selected num
  setopt localoptions noglobsubst noposixbuiltins pipefail 2> /dev/null
  selected=( $(fc -l 1 |
    FZF_DEFAULT_OPTS="--height ${FZF_TMUX_HEIGHT:-40%}
                      $FZF_DEFAULT_OPTS --tac -n2..,.. --tiebreak=index
                      --bind=ctrl-r:toggle-sort
                      $FZF_CTRL_R_OPTS
                      --expect=ctrl-x --query=${(q)LBUFFER} +m" $(__fzfcmd)) )
  local ret=$?
  if [ -n "$selected" ]; then
    local accept=0
    if [[ $selected[1] = ctrl-x ]]; then
      accept=1
      shift selected
    fi
    num=$selected[1]
    if [ -n "$num" ]; then
      zle vi-fetch-history -n $num
      [[ $accept = 1 ]] && zle accept-line
    fi
  fi
  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
  return $ret
}

zle     -N   fzf-history-widget
bindkey '^R' fzf-history-widget

export FZF_DEFAULT_OPTS='
  --height 20% --reverse --multi --inline-info
  --color fg:252,bg:233,hl:67,fg+:252,bg+:235,hl+:81
  --color info:144,prompt:161,spinner:135,pointer:135,marker:118
  --preview "(highlight -O ansi -l {} 2> /dev/null ||
              cat {} ||
	      (test -d {} && tree -C {})
             ) 2> /dev/null | head -200"
  --bind ctrl-k:kill-line
  --bind "?:toggle-preview"
'

export FZF_CTRL_R_OPTS='
  --preview "echo {}"
  --preview-window down:3:hidden
  --bind "?:toggle-preview"'
fi
