#!/usr/bin/env zsh
# -*- mode: Shell-script ;-*-

local return_code="%(?..%{$fg[red]%}%? ↵%{$reset_color%} )"

#
_prompt_jobs() {
    local jobs_status
    jobs_status=("")
    [[ $(jobs -l | wc -l) -gt 0 ]] && jobs_status+="%{%F{cyan}%}⚙"
    echo -n "$jobs_status"
}

_git_status() {
    local git_status
    git_status=("")
    if (( ${+commands[git-radar]} )) {
	   git_status=`git-radar --zsh --fetch`
    }
    echo -n "$git_status"
}

function _prompt_pwd {
  setopt LOCAL_OPTIONS
  setopt EXTENDED_GLOB
  # adapted from gh:sorin-ionescu/prezto
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]] {
    print -n "$MATCH"
    unset MATCH
  } else {
    print -n "${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  }
}

_prompt_where() {

  print -n "%{%f%b%k%}"
  if [[ -n $SSH_CLIENT || -n $REMOTEHOST || $USER != $LOGNAME || $(id -u) -eq 0 ]] {
    print -n "%{$fg[blue]%}%n%f@%{$fg[grey]%}%m%f"
  } else {
    print -n "%{$fg[grey]%}λ%f"
  }

  print -n " $(_prompt_pwd) %(!.%F{red}.%{$fg[grey]%})→%f"
}

setopt PROMPT_SUBST
RPROMPT='${return_code}$(_prompt_jobs)$(_git_status)'
PROMPT='$(_prompt_where) '
