#!/usr/bin/env/zsh
# -*- mode: Shell-script ;-*-

# auto completion tweaks
autoload -U compinit && compinit -u
zmodload -i zsh/complist

#############################################
# options

setopt completeinword   # not just at the end
setopt alwaystoend      # when complete from middle, move cursor

#############################################
# hosts
zstyle -e ':completion::*:hosts' hosts 'reply=($(sed -e "/^#/d" -e "s/ .*\$//" -e "s/,/ /g" /etc/ssh_known_hosts(N) ~/.ssh/known_hosts(N) 2>/dev/null | xargs) $(grep \^Host ~/.ssh/config(N) | cut -f2 -d\  2>/dev/null | xargs))'

# Hosts to use for completion, see later zstyle.
zstyle '*' hosts $(awk '/^[^#]/ {print $3" "$4" "$5}' /etc/hosts)

# ###########################################
# filename suffixes to ignore during completion, except after rm
# command.
zstyle ':completion:*:*:(^rm):*:*files' ignored-patterns \
    '*?.o' '*~' # '*?.orig'

#menu style completion
zstyle ':completion:*:*:*:*:*' menu select

#colors in completion
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

[ -z "$ck_default_completion" ] && {
  binding=$(bindkey '^I')
  [[ $binding =~ 'undefined-key' ]] || ck_default_completion=$binding[(s: :w)2]
  unset binding
}

#from zsh-users
expand-or-complete-with-dots() {
  echo -n "\e[31m âš™ \e[0m"
  zle ${ck_default_completion:-expand-or-complete}
  zle redisplay
}

zle -N expand-or-complete-with-dots
bindkey "^I" expand-or-complete-with-dots
